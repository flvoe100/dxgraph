def pomConfig = {
    licenses {
        license {
            name 'GNU General Public License, Version 3.0'
            url 'https://www.gnu.org/licenses/gpl-3.0.txt'
            distribution "repo"
        }
    }

    developers {
        developer {
            id 'stnot'
            name 'Stefan Nothaas'
            email 'stefan.nothaas@uni-duesseldorf.de'
        }

        developer {
            id 'KevinBeineke'
            name 'Kevin Beineke'
            email 'kevin.beineke@uni-duesseldorf.de'
        }

        developer {
            id 'phhere'
            name 'Philipp Rehs'
            email 'Philipp.Rehs@uni-duesseldorf.de'
        }
    }

    scm {
        url 'https://github.com/hhu-bsinfo/dxgraph'
    }
}

publishing {

    publications {

        mavenPublication(MavenPublication) {

            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }

            groupId project.group
            artifactId project.name
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'DXRam\'s Graph application')
                root.appendNode('name', 'DXGraph')
                root.appendNode('url', 'https://github.com/hhu-bsinfo/dxgradle')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    publish = false
    override = true

    pkg {
        repo = 'dxram'
        name = 'dxgraph'
        userOrg = 'hhu-bsinfo'
        licenses = ['GPL-3.0']
        websiteUrl = 'https://www.dxram.io'
        vcsUrl = 'https://github.com/hhu-bsinfo/dxgraph.git'
        labels = ['dxram', 'graph']
        githubRepo = 'hhu-bsinfo/dxgraph'
        //githubReleaseNotesFile = 'README.md'

        version {
            name = project.version
            released  = new Date()
        }
    }
}